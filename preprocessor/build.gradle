apply plugin: 'antlr'

ext.antlr = [
        grammarpackage: "com.tazadum.glsl.preprocessor.parser",
        antlrSource   : 'src/main/antlr',
        destinationDir: "src/generated-sources/java"
]

dependencies {
	api libs.jopt.simple
	api libs.slf4j
	api libs.antlr.runtime
	antlr libs.antlr.buildtime
	testImplementation project(':minime-test-util')
}

configurations {
	api {
		// Do not export the antlr4 build time dependency to the API configuration
		// Work around from: https://github.com/gradle/gradle/issues/820
		extendsFrom = extendsFrom.findAll { it.name != "antlr" }
	}
}

generateGrammarSource {
	arguments += ['-package', antlr.grammarpackage, '-visitor']
	outputDirectory = file("${projectDir}/generated-src/com/tazadum/glsl/preprocessor/parser")
}

task cleanAntlr(type: Delete) {
	delete file("${projectDir}/generated-src")
}
clean.dependsOn cleanAntlr

sourceSets.main.java.srcDirs += file("${projectDir}/generated-src")
